defmodule Mob.DeviceTest do
  use ExUnit.Case, async: false

  # Tests cover the GenServer fan-out logic without requiring the NIF.
  # The NIF stubs raise when not loaded; we verify the public-API exports
  # in a separate describe block and exercise the dispatcher by sending
  # synthetic OS messages.

  alias Mob.Device

  setup do
    # Start fresh dispatcher (and platform fan-outs it forwards to) per test.
    start_supervised!({Mob.Device.IOS, []})
    start_supervised!({Mob.Device.Android, []})

    {:ok, pid} =
      GenServer.start_link(Device, [], name: :"device_#{System.unique_integer([:positive])}")

    on_exit(fn ->
      Mob.Test.ProcessHelpers.stop_pid(pid)
    end)

    {:ok, dispatcher: pid}
  end

  describe "module exports" do
    # function_exported?/3 checks elsewhere were removed — the
    # "raises when NIF not loaded" tests below actually invoke each function,
    # which proves both existence and behaviour. A bare `function_exported?`
    # test gives a false sense of coverage and trips Credo's "this test
    # doesn't call any application code" warning.

    test "categories/0 returns the 7 known categories" do
      cats = Device.categories()
      assert :app in cats
      assert :display in cats
      assert :audio in cats
      assert :appearance in cats
      assert :power in cats
      assert :thermal in cats
      assert :memory in cats
    end
  end

  describe "category_for/1" do
    test "maps app events" do
      assert Device.category_for(:will_resign_active) == :app
      assert Device.category_for(:did_become_active) == :app
      assert Device.category_for(:did_enter_background) == :app
      assert Device.category_for(:will_enter_foreground) == :app
      assert Device.category_for(:will_terminate) == :app
    end

    test "maps display events" do
      assert Device.category_for(:screen_off) == :display
      assert Device.category_for(:screen_on) == :display
      assert Device.category_for(:orientation_changed) == :display
    end

    test "maps audio events" do
      assert Device.category_for(:audio_interrupted) == :audio
      assert Device.category_for(:audio_resumed) == :audio
      assert Device.category_for(:audio_route_changed) == :audio
    end

    test "maps power, thermal, memory events" do
      assert Device.category_for(:battery_state_changed) == :power
      assert Device.category_for(:battery_level_changed) == :power
      assert Device.category_for(:low_power_mode_changed) == :power
      assert Device.category_for(:thermal_state_changed) == :thermal
      assert Device.category_for(:memory_warning) == :memory
    end

    test "maps :color_scheme_changed to :appearance" do
      assert Device.category_for(:color_scheme_changed) == :appearance
    end

    test "maps :connectivity_changed to :network" do
      assert Device.category_for(:connectivity_changed) == :network
    end

    test ":network is a valid category and included in subscribe(:all)" do
      assert :network in Device.categories()
    end

    test "unknown events fall through to :unknown" do
      assert Device.category_for(:no_such_event) == :unknown
    end
  end

  describe "orientation lock" do
    test "valid_lock?/1 accepts the five lock values" do
      for o <- [:portrait, :portrait_upside_down, :landscape, :landscape_left, :landscape_right] do
        assert Device.valid_lock?(o), "expected #{o} to be a valid lock"
      end
    end

    test "valid_lock?/1 rejects anything else" do
      refute Device.valid_lock?(:unspecified)
      refute Device.valid_lock?(:sideways)
      refute Device.valid_lock?(nil)
    end

    test "lock_orientation/1 rejects an invalid value before touching the NIF" do
      # Invalid values short-circuit to {:error, :invalid} (the guard fails)
      # rather than raising nif_error, so this is safe to assert on the host.
      assert Device.lock_orientation(:sideways) == {:error, :invalid}
      assert Device.lock_orientation("landscape") == {:error, :invalid}
    end

    test "keep_awake/1 rejects a non-boolean before touching the NIF" do
      # The is_boolean/1 guard fails on the host before the NIF is reached,
      # so this is safe to assert without a loaded NIF.
      assert_raise FunctionClauseError, fn -> Device.keep_awake(:yes) end
      assert_raise FunctionClauseError, fn -> Device.keep_awake(1) end
    end
  end

  describe "open_settings/1" do
    test "rejects an unknown target before touching the NIF" do
      # Unknown targets short-circuit to {:error, :invalid} (no clause matches the
      # guard), so this is safe to assert on the host without a loaded NIF.
      assert Device.open_settings(:bogus) == {:error, :invalid}
      assert Device.open_settings("app") == {:error, :invalid}
      assert Device.open_settings(nil) == {:error, :invalid}
    end
  end

  describe "subscription fan-out" do
    test "subscriber receives events for its categories", %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), [:app]})
      send(d, {:mob_device, :did_enter_background})
      assert_receive {:mob_device, :did_enter_background}, 100
    end

    test "subscriber does not receive events outside its categories", %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), [:thermal]})
      send(d, {:mob_device, :did_enter_background})
      refute_receive {:mob_device, :did_enter_background}, 50
    end

    test "subscriber to :all (via list of all categories) gets everything", %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), Device.categories()})

      send(d, {:mob_device, :did_enter_background})
      assert_receive {:mob_device, :did_enter_background}, 100

      send(d, {:mob_device, :memory_warning})
      assert_receive {:mob_device, :memory_warning}, 100

      send(d, {:mob_device, :thermal_state_changed, :serious})
      assert_receive {:mob_device, :thermal_state_changed, :serious}, 100
    end

    test "events with payload are delivered with payload", %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), [:power]})
      send(d, {:mob_device, :battery_level_changed, 73})
      assert_receive {:mob_device, :battery_level_changed, 73}, 100
    end

    test ":appearance subscriber receives :color_scheme_changed with the scheme atom",
         %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), [:appearance]})

      send(d, {:mob_device, :color_scheme_changed, :dark})
      assert_receive {:mob_device, :color_scheme_changed, :dark}, 100

      send(d, {:mob_device, :color_scheme_changed, :light})
      assert_receive {:mob_device, :color_scheme_changed, :light}, 100
    end

    test ":appearance is filtered out for subscribers in unrelated categories",
         %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), [:thermal]})
      send(d, {:mob_device, :color_scheme_changed, :dark})
      refute_receive {:mob_device, :color_scheme_changed, :dark}, 50
    end

    test ":network subscriber receives :connectivity_changed with the state map",
         %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), [:network]})

      state = %{online: true, transport: :wifi, expensive: false}
      send(d, {:mob_device, :connectivity_changed, state})
      assert_receive {:mob_device, :connectivity_changed, ^state}, 100

      offline = %{online: false, transport: :none, expensive: false}
      send(d, {:mob_device, :connectivity_changed, offline})
      assert_receive {:mob_device, :connectivity_changed, ^offline}, 100
    end

    test ":connectivity_changed is filtered out for subscribers in unrelated categories",
         %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), [:thermal]})
      send(d, {:mob_device, :connectivity_changed, %{online: true, transport: :wifi}})
      refute_receive {:mob_device, :connectivity_changed, _}, 50
    end

    test "multiple subscribers all receive matching events", %{dispatcher: d} do
      parent = self()

      subscriber = fn ->
        :ok = GenServer.call(d, {:subscribe, self(), [:app]})
        send(parent, :subscribed)
        assert_receive {:mob_device, :did_become_active}, 200
        :got_it
      end

      task1 = Task.async(subscriber)
      task2 = Task.async(subscriber)

      # Both subscriptions must be registered before the event is sent. The
      # tasks say when that is true; sleeping only guessed at it.
      assert_receive :subscribed
      assert_receive :subscribed
      send(d, {:mob_device, :did_become_active})

      assert Task.await(task1) == :got_it
      assert Task.await(task2) == :got_it
    end

    test "unsubscribe removes the subscriber", %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), [:app]})
      :ok = GenServer.call(d, {:unsubscribe, self()})
      send(d, {:mob_device, :did_enter_background})
      refute_receive {:mob_device, :did_enter_background}, 50
    end

    test "subscriber pid going down is auto-removed", %{dispatcher: d} do
      task =
        Task.async(fn ->
          :ok = GenServer.call(d, {:subscribe, self(), [:app]})
          :done
        end)

      assert Task.await(task) == :done

      # The :DOWN comes from the monitor, not from this process, so a call here
      # orders nothing. Poll until the server has actually pruned the entry.
      Mob.Test.ProcessHelpers.eventually(fn ->
        subs = GenServer.call(d, :__test_subscribers__)
        not Map.has_key?(subs, task.pid)
      end)
    end

    test "double-subscribe replaces categories rather than duplicating", %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), [:app]})
      :ok = GenServer.call(d, {:subscribe, self(), [:thermal]})

      send(d, {:mob_device, :did_enter_background})
      refute_receive {:mob_device, :did_enter_background}, 50

      send(d, {:mob_device, :thermal_state_changed, :serious})
      assert_receive {:mob_device, :thermal_state_changed, :serious}, 100
    end
  end

  describe "platform forwarding" do
    test "iOS-tagged messages forward to Mob.Device.IOS", %{dispatcher: d} do
      Mob.Device.IOS.subscribe()

      send(d, {:mob_device_ios, :protected_data_will_become_unavailable})
      assert_receive {:mob_device_ios, :protected_data_will_become_unavailable}, 100
    end

    test "Android-tagged messages forward to Mob.Device.Android", %{dispatcher: d} do
      Mob.Device.Android.subscribe()

      send(d, {:mob_device_android, :doze_mode_changed, true})
      assert_receive {:mob_device_android, :doze_mode_changed, true}, 100
    end

    test "common-tagged subscribers do NOT receive platform-tagged messages",
         %{dispatcher: d} do
      :ok = GenServer.call(d, {:subscribe, self(), Device.categories()})

      send(d, {:mob_device_ios, :will_resign_active})
      refute_receive {:mob_device_ios, :will_resign_active}, 50
      refute_receive {:mob_device, :will_resign_active}, 50
    end
  end

  describe "Mob.Device.IOS subscription" do
    test "subscribe/0 and unsubscribe/0 work" do
      assert :ok = Mob.Device.IOS.subscribe()
      assert :ok = Mob.Device.IOS.unsubscribe()
    end

    test "subscriber pid down is removed" do
      task =
        Task.async(fn ->
          :ok = Mob.Device.IOS.subscribe()
          :done
        end)

      assert Task.await(task) == :done

      Mob.Test.ProcessHelpers.eventually(fn ->
        subs = GenServer.call(Mob.Device.IOS, :__test_subscribers__)
        not Map.has_key?(subs, task.pid)
      end)
    end
  end

  describe "Mob.Device.Android subscription" do
    test "subscribe/0 and unsubscribe/0 work" do
      assert :ok = Mob.Device.Android.subscribe()
      assert :ok = Mob.Device.Android.unsubscribe()
    end
  end

  describe "queries (NIF-backed, raise outside device)" do
    # These all delegate to :mob_nif.* which is not loaded in the test env.
    # Verifying the right exception is raised guards against accidental
    # pure-Elixir fallbacks.

    # credo's VacuousTest heuristic doesn't see `apply(Device, @fun, [])` as a
    # call into application code, but it is — through indirection.
    for fun <- [
          :battery_level,
          :battery_state,
          :thermal_state,
          :network_state,
          :online?,
          :os_version,
          :model
        ] do
      @fun fun
      # credo:disable-for-next-line Jump.CredoChecks.VacuousTest
      test "#{fun}/0 raises when NIF not loaded" do
        raised =
          try do
            apply(Device, @fun, [])
            false
          rescue
            ErlangError -> true
            UndefinedFunctionError -> true
          end

        assert raised, "expected Mob.Device.#{@fun}/0 to raise without the NIF"
      end
    end
  end

  describe "open_url/1" do
    # NIF-backed; the NIF stub raises in the test env. We exercise enough of
    # the wrapper to lock in input validation and the delegation contract.

    test "raises when NIF not loaded" do
      # credo:disable-for-next-line Jump.CredoChecks.VacuousTest
      raised =
        try do
          Device.open_url("https://example.com/")
          false
        rescue
          ErlangError -> true
          UndefinedFunctionError -> true
        end

      assert raised, "expected Mob.Device.open_url/1 to raise without the NIF"
    end

    test "FunctionClauseError on non-binary input" do
      assert_raise FunctionClauseError, fn -> Device.open_url(:not_a_binary) end
      assert_raise FunctionClauseError, fn -> Device.open_url(nil) end
      assert_raise FunctionClauseError, fn -> Device.open_url(123) end
    end

    test "spec accepts mailto, https, http, tel schemes" do
      # We can't actually launch on the test host (no NIF), but we can
      # confirm the wrapper accepts the URL forms our AndroidManifest
      # `<queries>` declares (mirrors mob_new template + air_cart_max).
      for url <- [
            "mailto:steve@example.com?subject=hi",
            "https://example.com/",
            "http://example.com/",
            "tel:+15551234567"
          ] do
        raised =
          try do
            # credo:disable-for-next-line Jump.CredoChecks.VacuousTest
            Device.open_url(url)
            false
          rescue
            ErlangError -> true
            UndefinedFunctionError -> true
          end

        assert raised, "expected open_url(#{inspect(url)}) to raise without the NIF"
      end
    end
  end
end
